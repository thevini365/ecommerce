plugins {
    id 'java'
    id 'com.bmuschko.docker-spring-boot-application' version '4.10.0'
    id "com.diffplug.gradle.spotless" version "3.23.1"
    id "com.github.ben-manes.versions" version "0.21.0"
    id 'se.patrikerdes.use-latest-versions' version '0.2.10'
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id 'net.researchgate.release' version '2.8.0'
    id 'org.springframework.boot' version '2.1.6.RELEASE'
}

apply plugin: 'io.spring.dependency-management'

group 'rocks.tmov'
version "$version"

sourceCompatibility = JavaVersion.VERSION_11

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'ea'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]$qualifier[.\d-+]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}
compileJava.dependsOn 'spotlessApply'

docker {
    springBootApplication {
        baseImage = 'adoptopenjdk/openjdk11:alpine'
        ports = [8080]
        tag = "$project.name:$version"
        jvmArgs = ['-Djava.security.egd=file:/dev/./urandom', '-Dspring.profiles.active=production', '-Xmx512m']
    }
}

dependencies {

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'

    testCompile group: 'junit', name: 'junit', version: '4.12'
}
